apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

repositories {
    mavenCentral()
    google()
}

configurations {
    compile.exclude module: 'support-v4'
}

configurations.all {
    exclude group: 'com.google.firebase', module: 'firebase-core'
    exclude group: 'androidx.recyclerview', module: 'recyclerview'
}

dependencies {
    implementation 'com.squareup.okhttp3:okhttp:5.0.0-alpha.2'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.6.10'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.2'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.2'

    implementation 'androidx.core:core-ktx:1.8.0-alpha03'
    implementation 'androidx.palette:palette-ktx:1.0.0'

    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.biometric:biometric:1.1.0'
    implementation 'com.google.android.material:material:1.6.0-alpha02'
    implementation 'androidx.exifinterface:exifinterface:1.3.3'
    implementation 'androidx.interpolator:interpolator:1.0.0'
    implementation 'androidx.viewpager:viewpager:1.0.0'
    implementation 'androidx.dynamicanimation:dynamicanimation:1.0.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation "androidx.sharetarget:sharetarget:1.1.0"
    implementation 'androidx.media:media:1.4.3'
    
    compileOnly 'org.checkerframework:checker-qual:3.18.1'
    compileOnly 'org.checkerframework:checker-compat-qual:2.5.5'

    compileOnly 'org.checkerframework:checker-qual:3.18.1'
    compileOnly 'org.checkerframework:checker-compat-qual:2.5.5'
    implementation 'com.googlecode.mp4parser:isoparser:1.0.6'
    implementation 'com.stripe:stripe-android:2.0.2'
    implementation 'org.osmdroid:osmdroid-android:6.1.10'
    implementation 'com.facebook.rebound:rebound:0.3.8'

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'


    implementation 'androidx.dynamicanimation:dynamicanimation-ktx:1.0.0-alpha03'
}

//return an api key from a properties file.
static def getAppId(){
    Properties properties = new Properties()
    properties.load(new FileInputStream("./API_KEYS"))
    return properties.get("APP_ID")
}

static def getAppHash(){
    Properties properties = new Properties()
    properties.load(new FileInputStream("./API_KEYS"))
    return "\"" + properties.get("APP_HASH") + "\""
}

android {
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
    }

    compileSdkVersion 31
    buildToolsVersion '30.0.3'
    ndkVersion "21.4.7075529"

    defaultConfig.applicationId = "org.nift4.catox"

    sourceSets.main.jniLibs.srcDirs = ['./jni/']

    externalNativeBuild {
        cmake {
            path 'jni/CMakeLists.txt'
        }
    }


	sourceSets.main {
        res.srcDirs = [
                'src/main/res-vkui',
                'src/main/res-launcher',
                'src/main/res'
        ]
    }

    dexOptions {
        jumboMode = true
        javaMaxHeapSize "2g"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8

        coreLibraryDesugaringEnabled true
    }

    signingConfigs {
        debug {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }

        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }
	packagingOptions {
		resources {
			excludes += ['DebugProbesKt.bin']
		}
	}


	buildTypes {
        debug {
            debuggable true
            jniDebuggable true
            signingConfig signingConfigs.debug
            applicationIdSuffix ".beta"
            minifyEnabled false
            shrinkResources false
            multiDexEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            ndk.debugSymbolLevel = 'FULL'
        }

        /*debugAsan {
            debuggable true
            jniDebuggable true
            signingConfig signingConfigs.debug
            applicationIdSuffix ".beta"
            minifyEnabled true
            multiDexEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            packagingOptions {
                doNotStrip "**.so"
            }

            sourceSets {
                main {
                    jniLibs {
                        srcDir {
                            'jniLibs'
                        }
                    }
                    resources {
                        srcDir {
                            'jniRes'
                        }
                    }
                }
            }
        }*/

        HA {
            debuggable false
            jniDebuggable false
            signingConfig signingConfigs.debug
            applicationIdSuffix ".beta"
            minifyEnabled true
            multiDexEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            ndk.debugSymbolLevel = 'FULL'
        }

        standalone {
            debuggable false
            jniDebuggable false
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            multiDexEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
        }

        debuggableRelease {
            debuggable true
            jniDebuggable true
            signingConfig signingConfigs.release
            minifyEnabled false
            shrinkResources false
            multiDexEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            ndk.debugSymbolLevel = 'FULL'
	        zipAlignEnabled true
        }

        release {
            debuggable false
            jniDebuggable false
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            multiDexEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
	        zipAlignEnabled true
        }

        foss {
            debuggable false
            jniDebuggable false
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            multiDexEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
        }
    }

    sourceSets.debug {
        manifest.srcFile 'config/debug/AndroidManifest.xml'
        java.srcDirs += ['src/foss/java']
    }

    /*sourceSets.debugAsan {
        manifest.srcFile 'config/debug/AndroidManifest.xml'
        java.srcDirs += ['src/foss/java']
    }*/

    sourceSets.HA {
        manifest.srcFile 'config/debug/AndroidManifest.xml'
        java.srcDirs += ['src/foss/java']
    }

    sourceSets.standalone {
        manifest.srcFile 'config/release/AndroidManifest_standalone.xml'
        java.srcDirs += ['src/play/java']

        dependencies {
            implementation 'com.google.firebase:firebase-messaging:23.0.0'
            implementation 'com.google.firebase:firebase-config:21.0.1'
            implementation 'com.google.firebase:firebase-datatransport:18.1.1'
            implementation 'com.google.firebase:firebase-appindexing:20.0.0'
            implementation 'com.google.mlkit:language-id:17.0.3'
        }
    }

    sourceSets.release {
        manifest.srcFile 'config/release/AndroidManifest.xml'
        java.srcDirs += ['src/play/java']

        dependencies {
            implementation 'com.google.firebase:firebase-messaging:23.0.0'
            implementation 'com.google.firebase:firebase-config:21.0.1'
            implementation 'com.google.firebase:firebase-datatransport:18.1.1'
            implementation 'com.google.firebase:firebase-appindexing:20.0.0'
            implementation 'com.google.mlkit:language-id:17.0.3'
        }
    }

    sourceSets.foss {
        manifest.srcFile 'config/release/AndroidManifest_foss.xml'
        java.srcDirs += ['src/foss/java']
    }

    flavorDimensions "minApi"

    productFlavors {
        armv7 {
            ndk {
                abiFilters "armeabi-v7a"
            }
            ext {
                abiVersionCode = 1
            }
        }
        x86 {
            ndk {
                abiFilters "x86"
            }
            ext {
                abiVersionCode = 2
            }
        }
        armv7_SDK23 {
            ndk {
                abiFilters "armeabi-v7a"
            }
            sourceSets.debug {
                manifest.srcFile 'config/debug/AndroidManifest_SDK23.xml'
            }
            sourceSets.release {
                manifest.srcFile 'config/release/AndroidManifest_SDK23.xml'
            }
            minSdkVersion 23
            ext {
                abiVersionCode = 3
            }
        }
        x86_SDK23 {
            ndk {
                abiFilters "x86"
            }
            sourceSets.debug {
                manifest.srcFile 'config/debug/AndroidManifest_SDK23.xml'
            }
            sourceSets.release {
                manifest.srcFile 'config/release/AndroidManifest_SDK23.xml'
            }
            minSdkVersion 23
            ext {
                abiVersionCode = 4
            }
        }
        arm64 {
            ndk {
                abiFilters "arm64-v8a"
            }
            ext {
                abiVersionCode = 5
            }
        }
        x64 {
            ndk {
                abiFilters "x86_64"
            }
            ext {
                abiVersionCode = 6
            }
        }
        arm64_SDK23 {
            ndk {
                abiFilters "arm64-v8a"
            }
            sourceSets.debug {
                manifest.srcFile 'config/debug/AndroidManifest_SDK23.xml'
            }
            sourceSets.release {
                manifest.srcFile 'config/release/AndroidManifest_SDK23.xml'
            }
            minSdkVersion 23
            buildConfigField 'String', 'APK_VARIANT', '"appArm64"'
            ext {
                abiVersionCode = 7
            }
        }
        x64_SDK23 {
            ndk {
                abiFilters "x86_64"
            }
            sourceSets.debug {
                manifest.srcFile 'config/debug/AndroidManifest_SDK23.xml'
            }
            sourceSets.release {
                manifest.srcFile 'config/release/AndroidManifest_SDK23.xml'
            }
            minSdkVersion 23
            ext {
                abiVersionCode = 8
            }
        }
        afat {
            ndk {
                abiFilters "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
            }
            sourceSets.debug {
                manifest.srcFile 'config/debug/AndroidManifest.xml'
            }
            sourceSets.release {
                manifest.srcFile 'config/release/AndroidManifest.xml'
            }
            sourceSets.standalone {
                manifest.srcFile 'config/release/AndroidManifest_standalone.xml'
            }
            ext {
                abiVersionCode = 9
            }
        }
    }

    defaultConfig.versionCode = 2566

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = "app.apk"
            output.versionCodeOverride = defaultConfig.versionCode * 10 + variant.productFlavors.get(0).abiVersionCode
        }
    }

    variantFilter { variant ->
        def names = variant.flavors*.name
        if (variant.buildType.name != "release" && !names.contains("afat")) {
            setIgnore(true)
        }
    }
	lint {
		disable 'MissingTranslation', 'ExtraTranslation', 'BlockedPrivateApi'
	}

	defaultConfig {
        minSdkVersion 16
        targetSdkVersion 30
        versionName "8.5.4"

        vectorDrawables.generatedDensities = ['mdpi', 'hdpi', 'xhdpi', 'xxhdpi']
        //obtain your own keys at https://core.telegram.org/api/obtaining_api_id
        buildConfigField 'int', 'APP_ID', getAppId()
        buildConfigField 'String', 'APP_HASH', getAppHash()
        buildConfigField 'String', 'APK_VARIANT', '"app"'

        externalNativeBuild {
            cmake {
                version '3.10.2'
                arguments '-DANDROID_STL=c++_static', '-DANDROID_PLATFORM=android-16', "-j=16"
            }
        }
    }
}

if (getGradle().getStartParameter().getTaskRequests().toString().contains("Release") || getGradle().getStartParameter().getTaskRequests().toString().contains("Standalone")) {
    apply plugin: 'com.google.gms.google-services'
}